plugins {
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.40" apply false
    id "org.jetbrains.kotlin.jvm" version "1.3.40" apply false
    id "org.junit.platform.gradle.plugin" version "1.0.3" apply false
    id "org.springframework.boot" version "2.0.4.RELEASE" apply false
}

def getCurrentGitTag() {
    try {
        def output = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '-l', '--points-at', 'HEAD'
            standardOutput = output
        }
        return output.toString().trim()
    }
    catch (ignored) {
        logger.warn('Current git tag retrieving has failed.')
        return ''
    }
}

def getLatestGitTag() {
    try {
        def output = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--abbrev=0', '--tags'
            standardOutput = output
        }
        return output.toString().trim()
    }
    catch (ignored) {
        logger.warn('Latest git tag retrieving has failed.')
        return ''
    }
}

version getCurrentGitTag() ?: getLatestGitTag() ?: System.env.VERSION ?: 'untagged'

def javaProjects = subprojects.findAll {
    it.name in ['common-jvm', 'cpp', 'fcodacy', 'fgithub', 'fgradle', 'fmoss', 'ftravis', 'git', 'githubql', 'model',
                'rest']
}

configure(javaProjects) {
    apply plugin: 'kotlin-platform-jvm'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://dl.bintray.com/jetbrains/spek" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        // Kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "org.jetbrains.kotlin:kotlin-reflect"

        // Arrow
        compile "io.arrow-kt:arrow-core:$arrow_version"

        // Test
        testImplementation 'org.amshove.kluent:kluent:1.38'
        testCompile "org.jetbrains.kotlin:kotlin-test"

        testCompile "org.jetbrains.spek:spek-api:$spek_version"
        testCompile "org.jetbrains.spek:spek-data-driven-extension:$spek_version"
        testCompile "org.jetbrains.spek:spek-subject-extension:$spek_version"
        testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"

        // Mock
        testCompile "com.nhaarman:mockito-kotlin:1.5.0"

        // Serialization
        compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.4'
        compile 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.1'

        // Log4j
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

        // Swagger
        compile "io.springfox:springfox-swagger2:$springfox_version"
        compile "io.springfox:springfox-swagger-ui:$springfox_version"
    }

    compileKotlin { kotlinOptions.jvmTarget = "1.8" }
    compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }
}

task version {
    doFirst {
        print project.version
    }
}
